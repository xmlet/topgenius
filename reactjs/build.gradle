plugins {
    id "com.github.node-gradle.node" version "7.0.1"
}

node {
    /* gradle-node-plugin configuration
       https://github.com/srs/gradle-node-plugin/blob/master/docs/node.md
       Task name pattern:
       ./gradlew npm_<command> Executes an NPM command.
    */
    // Version of node to use.
    version = '20.10.0'
    // Version of npm to use.
    npmVersion = '10.2.3'
    // If true, it will download node using above parameters.
    // If false, it will try to use globally installed node.
    download = false
}

npm_run_build {
    inputs.files fileTree('public')
    inputs.files fileTree('src')
    inputs.file 'package.json'
    inputs.file 'package-lock.json'
    outputs.dir 'build'
}

task packageTopgeniusReact(type: Zip) {
    dependsOn npm_run_build
    archiveBaseName = 'topgenius-reactjs'
    archiveExtension = 'jar'
    destinationDirectory =  file("${projectDir}/build_packageTopgeniusReact")
    from('build') {
        // optional path under which output will be visible in Java classpath, e.g. static resources path
        into 'public'
    }

    doFirst {
        ant.move file: "${projectDir}/build/index.html",
                 todir: 'build/react'
    }
}
